# Script de Monitoramento de Logs
# Versão 4.0

:local horaAgora [system clock get time]
:local data [system clock get date]
:local logs []

# Calcula o tempo limite para os logs (4 minutos atrás)
:local horaLimite [/system clock get time]
:set horaLimite ($horaLimite - 00:04:00)

# Busca logs dos últimos 4 minutos
:set logs [log find time>=$horaLimite]

:if ([:len $logs] > 0) do={
    :local mensagemTelegram ""

    :foreach i in=$logs do={
        :local mensagem [log get $i message]
        :local indexInicio ([:find $mensagem "<" -1] + 1)
        :local mensagemLimpa [:pick $mensagem $indexInicio [:len $mensagem]]
        :local topicos [log get $i topics]
        :if (!($mensagem ~ "telnet" and $mensagem ~ "logged in") and ($topicos ~ "pppoe")) do={
            :if ($mensagem ~ "authentication failed") do={
                :local horaAtual ([:pick $data 0 11] . " " . [log get $i time])
                # Adiciona todos os logs para o Telegram
                :set mensagemTelegram ($mensagemTelegram . "%F0%9F%94%B6 " . $horaAtual . " - " . $mensagemLimpa . "%0A%0A")
            }
        }
    }

    :if ([:len $mensagemTelegram] > 0) do={
        :local nomeSistema [system identity get name]
        :local Token "xxxxxxxxxxxxx"
        :local Chat "xxxxxxxxxxxxxx"
        :local mensagemFixadaTelegram

        # Adiciona Cabeçalho
        :set mensagemTelegram ("%E2%9C%85 [$nomeSistema] %E2%9C%85 %0A%0A %E2%99%A6 Alerta Logs:%E2%99%A6 %0A%0A " . $mensagemTelegram)

        :do {
            /tool fetch url="https://api.telegram.org/bot$Token/sendmessage?chat_id=$Chat&text=$mensagemTelegram" mode=https keep-result=no
        } on-error={
            # Adiciona uma mensagem de erro ao log do sistema
            :log error "Falha ao enviar mensagem para o Telegram."
        }
    }
}
